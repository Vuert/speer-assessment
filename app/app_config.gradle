ext {
    github_api_url = "https://api.github.com/"
    github_api_version_header = "2022-11-28"
    github_api_accept_header = "application/vnd.github+json"
}

ext.applyAppConfig = { config ->
    def props = new Properties()
    def propertiesFile = file("$rootDir${File.separator}api.properties")
    def githubApiKey = "githubApiKey"

    if (!propertiesFile.exists()) {
        throw new FileNotFoundException("Properties file not found")
    }

    propertiesFile.withReader { props.load(it) }

    if (!props.containsKey(githubApiKey)) {
        throw new IllegalStateException("GitHub api key not found")
    }

    config.buildConfigField 'String', "GITHUB_API_KEY", "\"${props.getProperty(githubApiKey)}\""
    config.buildConfigField 'String', "GITHUB_API_URL", "\"$github_api_url\""
    config.buildConfigField 'String', "GITHUB_API_VERSION", "\"$github_api_version_header\""
    config.buildConfigField 'String', "GITHUB_API_ACCEPT", "\"$github_api_accept_header\""
}
